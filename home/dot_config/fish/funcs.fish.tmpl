function f
    git ls-tree -r --name-only HEAD
end

function ef
    f | fzf | xargs -o -I% sh -c '$EDITOR %'
end

function last_history_item; echo $history[1]; end
abbr -a !! --position anywhere --function last_history_item

function git-review
    set -l N (git log --pretty=format:"%H %h" | grep -n (git merge-base origin/`git default-branch` HEAD) | cut -d : -f 1)
    git log --decorate --stat --reverse -p -$N
end
abbr -a gre 'git-review'

function tig-review
    set -l base (git merge-base origin/(git default-branch) HEAD)
    tig --reverse -w $base...HEAD
end

function touchp
    mkdir -p (dirname "$argv") && touch "$argv"
end

function copy-history -a historyNum
    set -q historyNum[1] || set historyNum 10
    history | head -n $historyNum | tac | pbcopy
end

# https://github.com/junegunn/fzf/wiki/Examples-(fish)
function fssh -d "Fuzzy-find ssh host via rg and ssh into it"
    rg --ignore-case '^host [^*]' ~/.ssh/config | cut -d ' ' -f 2 | fzf | read -l result; and ssh "$result"
end

function fs -d "Switch tmux session"
    tmux list-sessions -F "#{session_name}" | fzf | read -l result; and tmux switch-client -t "$result"
end

function checkout-git-branch -d "Fuzzy-find and checkout a branch"
    git branch | grep -v HEAD | string trim | fzf --preview="git log -200 --pretty=format:%s" | read -l result; and git checkout "$result"
    commandline -f repaint
end

function checkout-recent-git-branch -d "Fuzzy-find and checkout a branch"
    git for-each-ref --format="%(refname)" --sort=-committerdate | \
    grep -v HEAD | \
    grep -v refs/tags | \
    sed -e "s|^refs/heads/||" | \
    sed -e "s|^refs/remotes/||" | \
    string trim | \
    fzf --preview='' | \
    read -l result; and git checkout -t "$result"
    commandline -f repaint
end

# https://gist.github.com/azu/09dd6f27f52e2e8d9978
function mkdev -a dirName
    if ! test "$dirName"
        echo "Usage: mkdev dir-name"
        return
    end
    set -l rootDir (ghq root)
    set -l gitUser (git config user.name)
    set -l githubUser "github.com/$gitUser"
    set -l devPath "$rootDir/$githubUser/$dirName"

    mkdir -p $devPath
    cd $devPath
    git init
end

function mksandbox -a dirName
    if ! test "$dirName"
        echo "Usage: mksandbox dir-name"
        return
    end
    set -l rootDir (ghq root)
    set -l gitUser (git config user.name)
    set -l githubUser "github.com/$gitUser-sandbox"
    set -l devPath "$rootDir/$githubUser/$dirName"

    mkdir -p $devPath
    cd $devPath
    git init
end

function crrepo
    cd-gitroot
    set -l repoName (string match -r '[^/]*/[^/]*$' "$PWD")
    gh repo create $repoName --source=. $argv
end

function gwt -d "git work tree" -a branch
    set -l gitCdupDir (git rev-parse --show-cdup)
    git worktree add {$gitCdupDir}git-worktrees/$branch -b $branch
end

function wt
    # git worktree のディレクトリへ移動します
    #
    # [使い方]
    #   wt          : 全 worktree からインタラクティブに選択して移動します
    #   wt <query>  : ブランチ名やパスの一部で検索して移動します
    #                 - ヒットが1件のみ : 選択画面を出さずに即座に移動 (高速移動)
    #                 - ヒットが複数    : 入力値で絞り込まれた状態で選択画面を表示
    #                 - ヒットなし      : 何もせず終了

    set -l fzf_opts --height 40% --layout=reverse --border

    if count $argv > /dev/null
        set fzf_opts $fzf_opts --query "$argv" --select-1 --exit-0
    end

    set -l selected (git worktree list | fzf $fzf_opts)

    if test -n "$selected"
        set -l target_dir (echo $selected | string split -m1 " " | head -n1)
        cd $target_dir
    end
end

{{- if (eq .chezmoi.os "darwin") }}
function joinpdf
    mkdir -p formatted
    "/System/Library/Automator/Combine PDF Pages.action/Contents/Resources/join.py" --output formatted/output(date "+%Y%m%d_%H%M%S").pdf $argv
end

function hlcopy -d "Code highlighting" --argument language theme
    set -q language[1] || set language ts
    set -q theme[1] || set theme Zenburn
    pbpaste | highlight -S $language -s $theme -O rtf -k 'Source Han Code JP' -K 24 | pbcopy
end
{{- end }}
